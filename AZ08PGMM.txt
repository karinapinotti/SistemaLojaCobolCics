      *---------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *---------------------------------------------------------------*
       PROGRAM-ID                          AZ08PGMM.
      *---------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *---------------------------------------------------------------*
      *---------------------------------------------------------------*
       DATA                                DIVISION.
      *---------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *---------------------------------------------------------------*
      * COPY DA AREA DFHBMSCA - ATRIBUTOS
           COPY DFHBMSCA.
      * COPY DA AREA DFHAID - TECLAS PRESSIONADAS
           COPY DFHAID.
      * COPY DO MAPA SIMBOLICO AX08MNU
           COPY AZ08MNU.
      *
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).
      *---------------------------------------------------------------*
       LINKAGE                             SECTION.
      *---------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *---------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *---------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
               MAPFAIL(999-MAPFAIL)
               ERROR(999-ERROR)
           END-EXEC
      *---------------------------------------------------------------*
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL ZERO
              MOVE '1'                     TO WS-FASE
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 EXEC CICS SEND TEXT
                    FROM(WS-MSG-ERRO)
                    LENGTH(WS-LENGTH)
                    ERASE FREEKB ALARM
                 END-EXEC

                 EXEC CICS RETURN
                 END-EXEC
           END-EVALUATE
           .
      *---------------------------------------------------------------*
       100-FASE1.
      * LIMPAR AS VARIAVES DO MAPA
           MOVE LOW-VALUES                      TO MAPAMNUO
      * POSICIONAR O CURSOR NO CAMPO T1OPC
           MOVE -1                              TO T1OPCL
      * MOVER UMA MENSAGEM PARA O USUAIO
           MOVE 'ENTRE A OPCAO DESEJADA'        TO T1MSGO
      * DEPROTEGER CAMPO T1OPC
           MOVE DFHUNIMD                        TO T1OPCA
      * CARREGAR AS VARIAVEIS DO MAPA E ENVIAR
           PERFORM 999-MANDA-TELA
      * ENCERRAR A TRANSACAO E CHAMAR A PROXIMA FASE
           PERFORM 999-CHAMA-FASE2
           .
      *---------------------------------------------------------------*
       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER (210-ENTER)
              PF3   (220-PF3)
              PF5   (230-PF5)
              CLEAR (230-PF5)
              ANYKEY(240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP('MAPAMNU')
              MAPSET('AZ08MNU')
              INTO(MAPAMNUI)
           END-EXEC
           .
      *---------------------------------------------------------------*
       210-ENTER.
           IF T1OPCL = 0 OR T1OPCI = SPACES
              MOVE 'ESCOLHA UMA OPCAO VALIDA E PRESSIONE ENTER'
                                           TO T1MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           EVALUATE T1OPCI
              WHEN 'A'
                 MOVE '1'                       TO WS-FASE
                 EXEC CICS XCTL
                    PROGRAM('AZ08PGMA')
                    COMMAREA(WS-DFHCOMMAREA)
                    LENGTH(LENGTH OF WS-DFHCOMMAREA)
                 END-EXEC
              WHEN 'B'
                 MOVE '1'                       TO WS-FASE
                 EXEC CICS XCTL
                    PROGRAM('AZ08PGMB')
                    COMMAREA(WS-DFHCOMMAREA)
                    LENGTH(LENGTH OF WS-DFHCOMMAREA)
                 END-EXEC
              WHEN 'C'
                 MOVE '1'                       TO WS-FASE
                 EXEC CICS XCTL
                    PROGRAM('AZ08PGMC')
                    COMMAREA(WS-DFHCOMMAREA)
                    LENGTH(LENGTH OF WS-DFHCOMMAREA)
                 END-EXEC
              WHEN 'D'
                 MOVE '1'                       TO WS-FASE
                 EXEC CICS XCTL
                    PROGRAM('AZ08PGMD')
                    COMMAREA(WS-DFHCOMMAREA)
                    LENGTH(LENGTH OF WS-DFHCOMMAREA)
                 END-EXEC
              WHEN 'E' MOVE 'OPCAO E ESCOLHIDA' TO T1MSGO
              WHEN 'F' MOVE 'OPCAO F ESCOLHIDA' TO T1MSGO
              WHEN 'G' MOVE 'OPCAO G ESCOLHIDA' TO T1MSGO
              WHEN 'H' MOVE 'OPCAO H ESCOLHIDA' TO T1MSGO
              WHEN 'I' MOVE 'OPCAO I ESCOLHIDA' TO T1MSGO
              WHEN 'J' MOVE 'OPCAO J ESCOLHIDA' TO T1MSGO
              WHEN OTHER
                 MOVE 'OPCAO INVALIDA. TENTE NOVAMENTE'
                                           TO T1MSGO
           END-EVALUATE
           PERFORM 999-TRATA-FASE2
           .
      *---------------------------------------------------------------*
       220-PF3.
           MOVE 'OBRIGADO POR UTILIZAR NOSSA TRANSACAO'
                                           TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *---------------------------------------------------------------*
       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .
      *---------------------------------------------------------------*
       240-ANYKEY.
           MOVE 'TECLA INVALIDA - ESCOLHA UMA OPCAO VALIDA'
                                                TO T1MSGO
           PERFORM 999-TRATA-FASE2
           .
      *---------------------------------------------------------------*
       999-MANDA-TELA.
           MOVE EIBTRMID                        TO T1TERMO
           MOVE EIBTRNID                        TO T1TRANO
           MOVE EIBTASKN                        TO T1TASKO
           MOVE WS-FASE                         TO T1FASEO

           EXEC CICS ASSIGN
              USERID(T1USRO)
           END-EXEC

           MOVE +18                             TO WS-LENGTH

           EXEC CICS LINK
              PROGRAM('AUXCICS1')
              COMMAREA(WS-VAR-TEMPO)
              LENGTH(WS-LENGTH)
           END-EXEC

           MOVE WS-DATA                    TO T1DATAO
           MOVE WS-HORARIO                 TO T1HORAO

           EXEC CICS SEND
              MAP('MAPAMNU')
              MAPSET('AZ08MNU')
              FROM(MAPAMNUO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .
      *---------------------------------------------------------------*
       999-CHAMA-FASE1.
           MOVE '1'                        TO WS-FASE

           EXEC CICS RETURN
              TRANSID('Z08M')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *---------------------------------------------------------------*
       999-CHAMA-FASE2.
           MOVE '2'                        TO WS-FASE

           EXEC CICS RETURN
              TRANSID('Z08M')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
      *---------------------------------------------------------------*
       999-TRATA-FASE2.
           MOVE -1                         TO T1OPCL
           MOVE DFHUNIMD                   TO T1OPCA
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
      *---------------------------------------------------------------*
       999-ENCERRA-TRANSACAO.
           MOVE +80                        TO WS-LENGTH

           EXEC CICS SEND TEXT
              FROM  (WS-MSG-ERRO)
              LENGTH(WS-LENGTH)
              ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .
      *---------------------------------------------------------------*
       999-MAPFAIL.
           MOVE 'ERRO NO MAPA AZ08MNU'     TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *---------------------------------------------------------------*
       999-ERROR.
           MOVE 'ERRO NAO PREVISTO'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
      *---------------------------------------------------------------*
